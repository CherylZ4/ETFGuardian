plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.etf'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	swaggerCodegen 'org.openapitools:openapi-generator-cli:7.3.0'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

processResources {
	dependsOn(generateSwaggerCode)}

swaggerSources {
	openapi {
		inputFile = file("${rootDir}/src/main/resources/openapi.yaml")
		code {
			language = 'spring'
			configFile = file("${rootDir}/src/main/resources/config.json")

			//what to generate. Optional.
			components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
			dependsOn validation //first validate the yaml
		}
	}
}

//Set generated code to sourceSet to be able to used it in the implementation
compileJava.dependsOn swaggerSources.openapi.code
sourceSets.main.java.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/resources"

